module arrays

imports ds-signatures/Identifiers-sig
imports ds-signatures/Variables-sig
imports ds-signatures/Arrays-sig
imports dynamics/natives
imports dynamics/store
imports dynamics/base
imports dynamics/numbers
imports dynamics/bindings

signature
  sort aliases
    Idx = Map(Int, Int)
  variables
    I : Idx
  constructors  
    ArrayV : Idx -> V
    WrappedLV : LValue -> V
    WrappedInt : Int -> V
    WrappedIndex : Index -> V
  arrows    
    initArray(Int, Int, V, Idx) --> Idx
    getIndex(Index) --> Int

rules
    
  Array(_, IntV(i), v) --> ArrayV(I)
  where
    initArray(0, i, v, {}) --> I.
  
  initArray(i, j, v, I) --> I'
  where 
    case ltI(i, j) of {
      1 => 
        allocate(v) --> a;
        initArray(addI(i, 1), j, v, {i |--> a, I}) --> I'
      0 => 
        I => I'
    }.
  
  getIndex(__Exp2Index__(e)) --> i
  where
    e --> IntV(i).
  
  Subscript(p, q) --> v
  where
    p -lval-> arrayAddress;
    getIndex(q) --> i;
    read(arrayAddress) --> ArrayV(map);
    map[i] => address;
    read(address) --> v.
    