module dynamics/arrays

imports ds-signatures/Identifiers-sig
imports ds-signatures/Variables-sig
imports ds-signatures/Arrays-sig
imports dynamics/natives
imports dynamics/store
imports dynamics/base
imports dynamics/numbers
imports dynamics/bindings
imports dynamics/nabl2-link

signature
  constructors  
    ArrayV : Frame -> V

  arrows    
    initArray(Int, Int, V) --> U
    mkArrayIndex(Frame, Int) --> Addr

  constructors
    NoScope: Scope
    IdxOccurrence: Int -> Occurrence

rules
  
  default(ARRAY(_, _)) --> UndefV()
  
  Array(_, e_len, e_init) --> ArrayV(F)
  where
    e_len --> IntV(len);
    
    e_init --> v;
    // TRY: creating a scope for array and emulate the analysis (add resolution)
    initFrame(NoScope(), {}, {}) --> F;
    F |- initArray(0, len, v) --> _
 
  F |- initArray(i, j, v) --> U()
  where
    case ltI(i, j) of {
      1 =>
        set(mkArrayIndex(F, i), v) --> _;
        initArray(addI(i, 1), j, v) --> _
      0 =>
    }
  
  mkArrayIndex(F, i) --> mkAddr(F, IdxOccurrence(i))
  
