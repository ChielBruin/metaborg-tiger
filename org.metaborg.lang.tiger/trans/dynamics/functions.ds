module functions

imports ds-signatures/Functions-sig
imports dynamics/base
imports dynamics/store
imports dynamics/strings
imports dynamics/bindings
imports dynamics/natives
imports dynamics/numbers

signature
  sorts
    ID
  constructors
    ClosureV : List(FArg) * Exp * Env -> V
    __Id2ID__ : String -> Id {implicit}
    ID : String -> ID
  arrows
    E |- funEnv(List(FunDec)) :: H --> Env :: H
    E |- evalFuns(List(FunDec)) :: H --> Env :: H       
    E |- evalArgs(List(FArg), List(Exp)) :: H --> Env :: H
  
native operators
  debug : String -> V

rules // function definition

  FunDecs(fds) --> E
  where 
    funEnv(fds) --> E; 
    E |- evalFuns(fds) --> _. 
  
  E |- funEnv([]) --> E.  
  
  funEnv([FunDec(f, _, _, _) | fds]) --> E
  where 
    E bindVar(f, UndefV()) |- funEnv(fds) --> E.
  
  E |- evalFuns([]) --> E.
  
  E |- evalFuns([FunDec(f, args, _, e) | fds]) --> evalFuns(fds)
  where 
    writeVar(f, ClosureV(args, e, E)) --> _.

rules // function call
  
  // Predefined functions
  Call("print", [StringV(s)]) --> UnitV() where printS(s) => _.
  Call("ord", [StringV(s)]) --> IntV(ordS(s)).
  Call("chr", [IntV(i)]) --> StringV(chrI(i)).
  Call("not", [IntV(i)]) --> IntV(notI(i)).
  Call("exit", [IntV(i)]) --> UnitV() where exitI(i) => _.
  Call("size", [StringV(s)]) --> IntV(sizeS(s)).
  Call("substring", [StringV(s), IntV(i1), IntV(i2)]) --> StringV(substringSII(s, i1, i2)).
  Call("concat", [StringV(s1), StringV(s2)]) --> StringV(concatS(s1, s2)).
  Call("getchar", []) --> StringV(getcharS("")).
  Call("flush", []) --> UnitV() where flushS("") => _.
  
  // User-defined functions
  Call(f, es) --> v
  where
    readVar(f) --> ClosureV(args, e, E);
    evalArgs(args, es) --> E';
    E {E', E} |- e --> v.
    
  evalArgs([], []) --> {}.
    
  evalArgs([FArg(x, _) | args], [v | es]) --> {x |--> a, E}
  where 
    allocate(v) --> a; 
    evalArgs(args, es) --> E.
    
rules // procedure definition
 
  funEnv([ProcDec(f, _, _) | fds]) --> E
  where 
    E bindVar(f, UndefV()) |- funEnv(fds) --> E.
 
  E |- evalFuns([ProcDec(f, args, e) | fds]) --> evalFuns(fds)
  where 
    writeVar(f, ClosureV(args, e, E)) --> _.
    