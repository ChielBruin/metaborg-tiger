module dynamics-frames-directed/stdlib/exceptions

imports
  dynamics-frames-directed/stdlib/native/shared-terms

signature
  
  sorts
    _Thrown
    _Throwing
    _Catching
    _ExcVal
  
  constructors
    OK: _Thrown
  
  components
    _Exc: _Thrown = OK()
  
  sorts _handle_meta
  
  constructors
    handle: _Throwing * _Catching -> _Throwing
    handle: _Throwing * _Catching * _Throwing -> _Throwing
    
  arrows
    _Throwing --> _ExcVal
    _Throwing --> _ExcVal
    _Throwing --> _ExcVal
    handler(_Thrown, _Catching) --> _ExcVal
    raise(_Thrown) --> U

rules

  raise(thrown) :: _Exc OK() --> ??? :: _Exc thrown 
  
  handle(throwing, catching) :: _Exc OK() --> ev :: _Exc
  where
    throwing :: _Exc OK() --> ev1 :: _Exc1;
    case _Exc1 of {
      OK() =>
        ev1 => ev;
        _Exc1 => _Exc
      otherwise =>
        handler(_Exc1, catching) :: _Exc OK() --> ev :: _Exc
    }
  
  handle(throwing, catching, continuing) :: _Exc OK() --> ev :: _Exc
  where
    throwing :: _Exc OK() --> _ :: _Exc1;
    case _Exc1 of {
      OK() =>
        continuing :: _Exc OK() --> ev :: _Exc
      otherwise =>
        handler(_Exc1, catching) :: _Exc OK() --> ev :: _Exc
    }

