module dynamics-frames-directed/records


imports
  dynamics-frames-directed/base
  dynamics-frames-directed/types

signature
  constructors  
    NilV : V 
    RecordV: Frame -> V
  arrows
    Frames1 |- Map2-InitField(List(InitField)) --> U
    InitField --> U
    
rules // record type

  default(RECORD(_)) --> NilV()
  
rules // literals
  
  NilExp() --> NilV()

rules // record creation
  
  F F |- r@Record(t, inits) --> RecordV(F_rec)
  where
    F F |- t --> RECORD(s_rec);
    // this is actually the static (shared) frame of the record
    initDefFrame(s_rec, []) --> F_rec;
    initDefFrame(bodyScope(r), [I(F_rec)]) --> F_use;
    Frames1 (F_use, F) |- Map2-InitField(inits) --> _
  
  Frames1 (F, F') |- Map2-InitField([]) --> U()
  
  Frames1 (F, F') |- Map2-InitField([x | xs]) --> U()
  where
    Frames1 (F, F') |- x --> _;
    Frames1 (F, F') |- Map2-InitField(xs) --> _
  
  Frames1 (F_use, F) |- InitField(x : Occurrence, e) --> U()
  where
    F F |- e --> v;
    F F_use |- lookup(pathOf(x)) --> addr;
    set(addr, v) --> _
 
rules // record field access

  F F |- fv@FieldVar(e, f : Occurrence) --> addr_field
  where
    F F |- e --> addr_rec;
    get(addr_rec) --> RecordV(F_rec);
    initDefFrame(bodyScope(fv), [I(F_rec)]) --> F_use;
    F_use |- lookup(pathOf(f)) --> addr_field

