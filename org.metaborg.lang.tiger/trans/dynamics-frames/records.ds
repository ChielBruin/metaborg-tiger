module dynamics-frames/records


imports ds-signatures/Records-sig 
imports dynamics-frames/nabl2-link
imports dynamics-frames/base
imports dynamics-frames/store
imports dynamics-frames/unit

signature
  constructors  
    NilV : V 
    RecordV: Frame -> V
  arrows    
    initFields(List(InitField)) --> U
    
rules // records
  
  default(RECORD(_)) --> NilV()
  
  NilExp() --> NilV()
  
  F |- r@Record(Tid(ty : Occurrence), fields) --> RecordV(F_use)
  where
    typeOf(declOfPath(pathOf(ty))) --> RECORD(s_rec);
    initDefault(s_rec, {}) --> F_rec;
    bodyScope(r) --> s_use;
    initFrame(s_use, { I() |--> { s_rec |--> F_rec } }, {}) --> F_use;
    FS (F_use, F) |- initFields(fields) --> _
    
  initFields([]) --> U()
 
  FS (F_use, F) |- initFields([InitField(f : Occurrence, e) | fields]) --> U()
  where
    F |- e --> v;
    F_use |- lookup(pathOf(f)) --> addr;
    set(addr, v) --> _;
    FS (F_use, F) |- initFields(fields) --> _
    
  FieldVar(lv, x : Occurrence) -lvalf-> addr
  where
    lv -lvalf-> addr;
    get(addr) --> RecordV(F_use);
    F_use |- lookup(pathOf(x)) --> addr



