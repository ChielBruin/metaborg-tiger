module dynamics-frames-native/nabl2/arrays-native

imports
  dynamics-frames-native/nabl2/frames-native

signature
  native datatypes
    "org.metaborg.meta.lang.dynsem.interpreter.nabl2.f.arrays.Array" as Array {
      lookup: Int -> Addr
      get: Int -> Val
      set: Int * Val -> Val
      equals: Array -> Bool
    }
    
  sort aliases
    Length = Int
    
  native operators
    _newArray: Length * Type * Val -> Array // make array and fill with value
    
  arrows
    newArray(Length, Type) --> Array
    newArray(Length, Type, Val) --> Array
    arrayGet(Array, Int) --> Val 
    arraySet(Array, Int, Val) --> Val
    arraySubscript(Array, Int) --> Addr
    
rules

  newArray(length, ty) --> newArray(length, ty, default(ty))

  newArray(length, ty, val) --> _newArray(length, ty, val)

  arrayGet(array, idx) --> get[array](idx)
  
  arraySet(array, idx, val) --> set[array](idx, val)
  
  arraySubscript(array, idx) --> lookup[array](idx)

  